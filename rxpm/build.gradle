buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'consumer-proguard-rules.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {

    // For default implementations
    compileOnly rootProject.supportAppCompat
    compileOnly rootProject.supportDesign
    compileOnly rootProject.conductor
    compileOnly rootProject.googleMaps

    // Rx
    api rootProject.rxJava2
    implementation rootProject.rxRelay2
    implementation rootProject.rxAndroid2

    // RxBindings
    implementation rootProject.rxBindingKotlin
    implementation(rootProject.rxBindingSupportV4Kotlin) {
        exclude module: 'support-core-ui'
    }

    // Persistent PM
    implementation rootProject.outlast

    // For tests
    testImplementation rootProject.junitKotlin
    testImplementation rootProject.mockitoKotlin
    testImplementation rootProject.conductor
    testImplementation rootProject.supportAppCompat
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'RxPM'
    publishedGroupId = 'me.dmdev.rxpm'
    artifact = 'rxpm'
    libraryVersion = '1.2.4'
    gitUrl = 'https://github.com/dmdevgo/RxPM'
    allLicenses = ['MIT']
}

// Configuration of the library uploading to the Bintray
// Note: Call 'bintrayUpload' task (it will execute 'install' task first)
apply from: 'androidmaven.gradle'
apply from: 'bintray.gradle'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}
